
ACTOR sfDoomImp : FPlus_Monster replaces DoomImp
{
	spawnid 5
	obituary "%o was burned by an imp."
	hitobituary "%o was slashed by an imp."
	health 60
	radius 20
	height 56
	mass 100
	speed 8
	painchance 200
	seesound "imp/sight"
	painsound "imp/pain"
	deathsound "imp/death"
	activesound "imp/active"
	MONSTER
	+FLOORCLIP
	states
	{
		Spawn:
			TROO AB 10 A_Look
			goto Spawn
		See:
			TROO AABBCCDD 3 A_Chase
			goto See
		Melee:
		Missile:
			TROO EF 8 A_FaceTarget
			TROO G 6 A_TroopAttack  // See DoomImpBall
			goto See
		Pain:
			TROO H 2
			TROO H 2 A_Pain
			goto See
			
		MonDeath:
			TROO I 2
			TROO I 8 
			TROO J 8 A_Scream
			TROO K 6
			TROO L 6 A_NoBlocking
			TROO M 1
			goto Super::FadeOut
		
		MonXDeath:
			POSS A 0 A_SpawnItem ("splatEmitter_Lite" , 0, 0, 0, 0)
			TROO N 2
			TROO N 5 
			TROO O 5 A_XScream
			TROO P 5
			TROO Q 5 A_NoBlocking
			TROO RST 5
			TROO U 1
			goto Super::FadeOut
			
		MonRaise:
			TROO ML 8
			TROO KJI 6
			goto See
	}
}
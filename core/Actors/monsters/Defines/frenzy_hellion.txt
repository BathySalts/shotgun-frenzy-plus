//Hellion
actor Hellion : FPlus_Monster 30132
{
spawnid 207
  obituary "A Hellion burned %o was to a crisp."
  health 110
  radius 20
  height 56
  mass 120
  speed 14
  painchance 24
  seesound "monster/hlnsit"
  painsound "monster/hlnpai"
  deathsound "monster/hlndth"
  activesound "monster/hlnact"
  +FLOORCLIP
  states
  {
  Spawn:
    HELN AB 10 A_Look
    goto Spawn
  See:
    HELN AABBCCDDEEFF 2 A_Chase
    goto See
  Missile:
    HELN G 0 A_VileStart
    HELN GHIJK 6 Bright A_FaceTarget
    HELN L 6 Bright A_CustomMissile("HellionBall",32,0,0,0,0)
    HELN L 2
    goto See
  Pain:
    HELN M 2
    HELN M 2 A_Pain
    goto See
	
  MonDeath:
    HELN N 6
    HELN O 6 A_Scream
    HELN PQR 6
    HELN S 6 A_NoBlocking
    HELN S 650
  goto Super::FadeOut
  
  MonXDeath:
    HELN U 5
    HELN V 5 A_XScream
    HELN W 5
    HELN X 5 A_NoBlocking
	HELN Y 5
	HELN Z 5 
	HELN "[" 5 
	HELN "\" 5 
	HELN "]" 5 
  goto Super::FadeOut
  
  MonRaise:
    HELN TSRQPON 6
    goto See
  }
}

ACTOR HellionBall : CacodemonBall
{
Damage 11
Speed 20
Alpha 0.80
DamageType fire
+THRUGHOST
+FORCEXYBILLBOARD
SeeSound "Monster/hlnatk"
DeathSound "Monster/hlnexp"
+MthruSpecies
 states
  {
  Spawn:
    HLBL AB 3 bright A_SpawnItemEx("HellionPuff",0,0,0,0,0,0)
    goto Spawn
  Death:
    HLBL JKLMN 3 bright
    stop
  }
}

ACTOR HellionPuff
{   
   Radius 3
   Height 3
   RENDERSTYLE ADD
   Alpha 0.67
   +NOGRAVITY
   +NOBLOCKMAP
   +DONTSPLASH
   +FORCEXYBILLBOARD
   +CLIENTSIDEONLY
   +NOINTERACTION
   States
   {
   Spawn:
      NULL A 3 Bright
      HLBL CDEFGHI 3 BRIGHT
      Stop 
   }
}
//End of Hellion
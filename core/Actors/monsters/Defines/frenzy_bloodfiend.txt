ACTOR Bloodfiend : FPlus_Monster 30100 
{ 
	spawnid 238
	Health 400
	PainChance 140
	Speed 12 
	Radius 15 
	Height 56 
	Mass 450 
	+FloorClip
	+LongMeleeRange
	OBITUARY "%o died from the toxic blood of a bloodfiend."
	HITOBITUARY "%o was eaten by a blood fiend."
	SEESOUND "monster/sg2sit" 
	ATTACKSOUND "monster/sg2atk" 
	PAINSOUND "demon/pain" 
	DEATHSOUND "demon/death" 
	ACTIVESOUND "demon/sg2act"
	meleerange 64
	states 
		{ 
		Spawn: 
		   SAR2 AB 10 A_Look 
		   goto Spawn 
		See: 
		   SAR2 AABBCCDD 2 A_Chase 
		   goto See 
		Missile:
		   SAR2 A 0 A_JumpIfcloser(300, "Melee")
		   SAR2 GF 8 A_FaceTarget 
		   SAR2 E 0 A_PlaySound ("Weapons/bloodf")
		   SAR2 E 0 A_CustomMissile ("Bloodshot",22,0,-1,0,0)
		   SAR2 E 8 A_CustomMissile ("Bloodshot",22,0,1,0,0)
		   Goto See
		Melee:
			SAR2 E 8 A_FaceTarget
			TNT1 A 0 ThrustThingZ(0,24,0,0)
			TNT1 A 0 A_Recoil(-20)
			TNT1 A 0 A_FaceTarget
			SAR2 E 0 A_PlaySound("blooddemon/melee", CHAN_AUTO)
			SAR2 F 4 A_CustomMeleeAttack(random(2, 15))
			
			TNT1 A 0 A_Recoil(-5)
			TNT1 A 0 A_FaceTarget
			SAR2 F 4 A_CustomMeleeAttack(random(2, 15))
			
			TNT1 A 0 A_Recoil(-5)
			TNT1 A 0 A_FaceTarget
			SAR2 G 4 A_CustomMeleeAttack(random(2, 15))
			
			TNT1 A 0 A_Recoil(-2)
			TNT1 A 0 A_FaceTarget
			SAR2 G 4 A_CustomMeleeAttack(random(2, 15))
			goto See
		Pain: 
		   SAR2 H 2 
		   SAR2 H 2 A_Pain 
		   Goto See 
		   
		MonDeath: 
		   SAR2 I 8 
		   SAR2 J 8 A_Scream 
		   SAR2 K 4 
		   SAR2 L 4 A_NoBlocking 
		   SAR2 M 4 
		   SAR2 N 650
		goto Super::FadeOut

		MonXDeath:
		   SAR2 O 5
		   SAR2 P 5 A_XScream
		   SAR2 Q 5 A_NoBlocking
		   SAR2 RSTUV 5
		   SAR2 W 650
		goto Super::FadeOut

		MonRaise: 
		   SAR2 NMLKJI 5 
		   Goto See 
		} 
}

ACTOR Bloodshot
{
   Radius 3
   Height 3
   Speed 20
   Damage 2
   PROJECTILE
   RENDERSTYLE TRANSLUCENT
   ALPHA 0.80
   +THRUGHOST
   +mthruspecies
   DeathSound "weapons/bloodx"
   States
   {
   Spawn:
      FBLD A 2 Bright A_CStaffMissileSlither
      FBLD A 0 A_SpawnItem("Bloodtrail",0,0)
      FBLD B 2 Bright A_CStaffMissileSlither
      FBLD B 0 A_SpawnItem("Bloodtrail",0,0)
      goto Spawn
   Death:
      FBLD CDE 3 Bright
      stop
   }
}

ACTOR Bloodtrail
{   
   Radius 1
   Height 1
   PROJECTILE
   +CLIENTSIDEONLY
   +NOINTERACTION
   RENDERSTYLE TRANSLUCENT
   ALPHA 0.67
   States
   {
   Spawn:
      NULL A 3
      FBLD FGH 3 BRIGHT
      Stop 
   }
}

ACTOR Lurker : Bloodfiend 30101 
{
RENDERSTYLE TRANSLUCENT
Obituary "%o didn't see the lurker that was shooting at him."
HitObituary "%o didn't see the lurker right in front of him."
DropItem BloodAmmo
ALPHA 0.25
SpawnID 171
}
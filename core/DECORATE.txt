
Actor Integer : Inventory { +Undroppable Inventory.MaxAmount 0x7fffffff }
Actor Boolean : Inventory { +Undroppable Inventory.MaxAmount 1 }

#include "Actors/sfrenzy14.dec"
#include "Actors/acsutils.dec"
#include "Actors/samuterm.dec"
#include "Actors/prices.dec"
#include "Actors/huditems.dec"

#include "Actors/monsters/frenzyup_misc.txt"
#include "Actors/gameobjects/fpatch_armor.dec"

#include "Actors/projectiles/frenzy_monsterproj.txt"
#include "Actors/weapons/frenzy_healnstock.txt"
#include "Actors/projectiles/fplus_flak.txt"



ACTOR ZombieFodder : sfFastZombie replaces sfFastZombie 
{
  Health 5 // Just a futile resistance
  GibHealth 25
  Radius 10
  Height 56
  Speed 15
  PainChance 256
  Monster
  +FLOORCLIP +FASTER +FASTMELEE
  -THRUSPECIES // [Samu] Just to ease you the presure of running while those bastards destroys the core.
  SeeSound "ZombieFodder/sight"
  PainSound "ZombieFodder/pain"
  DeathSound "ZombieFodder/death"
  MeleeSound "ZombieFodder/melee"
  ActiveSound "ZombieFodder/active"
  Obituary "%o joins the zombies."
  
  States
  {
  Spawn:
    ZFOD AB 10 A_Look
    Loop
  See:
    ZFOD AABBCCDD 2 A_Chase
    Loop
  Melee:
    ZFOD EF 8 A_FaceTarget
    ZFOD G 8 A_CustomMeleeAttack(random(1, 4) * 6)
    Goto See
  Pain:
    ZFOD H 2
    ZFOD H 2 A_Pain
    Goto See
  Death:
    ZFOD I 2 A_Scream
	NULL A 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
	ZFOD I 5 ACS_ExecuteAlways(500, 0, 16, 0)
    ZFOD J 5 
    ZFOD K 5 A_NoBlocking
    ZFOD L 5
    ZFOD M 650
  DeathFade:
	ZFOD M 2 A_FadeOut(0.1)
	TNT1 A 2
	loop
  XDeath:
	ZFOD A 0 A_SpawnItem ("splatEmitter_Lite" , 0, 0, 0, 0)
	ZFOD N 2 A_XScream
	NULL A 0 A_SpawnItemEx("ClassicCreditBoom", 0, 0, 0, 0, 0, 0, 0, 0, 235)
	ZFOD N 5 
	ZFOD O 5 A_NoBlocking
	ZFOD PQR 5
	ZFOD S 650
  XDeathFade:
	ZFOD S 2 A_FadeOut(0.1)
	TNT1 A 2
	loop
  Raise:
    ZFOD L 5
    ZFOD KJI 5
    Goto See
  }
}

///////////////////////////////////////////////////
//      SHOTGUN FRENZY PLUS
//   a Shotgun Frenzy fork by Samuzero15tlh

// FP_CredD.acs
// Money droppin between players!

// Drop sum money!
#library "FP_CredD"

#import "fl_techs.acs"
#import "f_intdb.acs"
#include "zcommon.acs"
#include "acsutils.ach"
#include "samu_tools.ach"

int p_DropCredits[32];

#define DROPCREDIT_MIN 100
#define DROPCREDIT_MAX 9999999
#define DROPCREDIT_MUL 1000000

Script "SFPlus_DropCredits" (int amount) NET
{
	str mod_name = "(sfp_dropcredits): ";
	if(!GetCvar("sfp_allowdropcredits")){ // Check cvar.
		log(s:mod_name, s:"sfp_allowdropcredits is disabled on the server.");
		terminate;
	}
	if(amount == 0){// Check parameter
		log(s:mod_name, s:"Missing the amount argument.");
		terminate;
	}
	if(amount < DROPCREDIT_MIN || amount > DROPCREDIT_MAX){// Check range
		log(s:mod_name, s:"You can't drop that amount of credits.");
		log(s:mod_name, s:"Use a value between ", d:DROPCREDIT_MIN, s:" and ", d:DROPCREDIT_MAX);
		terminate;
	}
	if(amount > p_Credits[playernumber()]){// Check amount.
		log(s:mod_name, s:"You can't drop a bigger amount than the credits you have.");
		terminate;
	}
	p_credits[playernumber()] -= amount;
	
	int utid = UniqueTID();
	int x = GetActorX(0) + FixedMul(cos(getActorAngle(0)), 128.0);
	int y = GetActorY(0) + FixedMul(sin(getActorAngle(0)), 128.0);
	int z = GetActorZ(0) + 16.0;
	
	int amount_spawned = 0;
	int amount_to_drop = amount;
	int multipler = DROPCREDIT_MUL;
	while (amount_spawned != amount){
		if(amount_to_drop >= multipler){
			Spawn(strparam(s:"DropCredits_", d:multipler), x, y, z);
			amount_to_drop-=multipler;
			amount_spawned+=multipler;
		}else {multipler /= 10; delay(1);}
	}
	Spawn("DropCredits", x, y, z);
	
	log(d:amount ,s:" credits dropped.");
}

Script "SFPlus_AddDroppedCredits" (int howmuch){
	sf_AddCredits_S(howmuch, playernumber());
	p_DropCredits[playernumber()] += howmuch; // For the displayer.
}

Script "SFPlus_DisplayChange_Respawn" RESPAWN{
	ACS_NamedExecute("SFPlus_DisplayChange", 0);
}

Script "SFPlus_DisplayChange" ENTER {
	// This loop is here to show the real amount of the credits you got.
	int last = 0;
	int count = 0;
	int play = playernumber();
	while(GetActorProperty(0,APROP_HEALTH) > 1){
		if(last != p_DropCredits[play] && p_DropCredits[play] > last){
			ACS_NamedExecuteAlways("SF_CreditAddDisplay", 0, p_DropCredits[play], 5, 1);
			last = p_DropCredits[play];
		}
		else{
			if(p_DropCredits[play] != 0) count++;
			if(count == 35*3){ 
				p_DropCredits[play] = 0; 
				last = 0;
				count = 0;
			}
		}
		
		delay(1);
	}
}

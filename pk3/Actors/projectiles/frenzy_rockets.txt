//This goes in frenzy_weapons. wad and it replaces the DW8 lump
ACTOR Rocket_Standard
{
	Radius				4
	Height				8
	Speed				32
	Damage				35
	DamageType			MarineFire2
	Projectile
	+RANDOMIZE
	+ROCKETTRAIL
	+FORCERADIUSDMG
	+MTHRUSPECIES
	SeeSound			"weapons/rocklf"
	DeathSound			"weapons/rocklx"
	Obituary			"%o rode %k's rocket."
	
	States
	{
		Spawn:
			MISL A 1 bright
			goto Spawn
		Death:
			MISL B 8 bright A_Explode(150,160,0,0,64)
			MISL C 6 bright
			MISL D 4 bright
			stop
	}
}

ACTOR Rocket_Exploder //[Samu] Exploder my ass!
{
	Radius				4
	Height				8
	Speed				68
	Damage				40
	
	DamageType			MarineFire2
	
	Projectile
	+RANDOMIZE
	+ROCKETTRAIL
	+MTHRUSPECIES
	//[Samu] Ripping + Explosions = A massive push to the monsters..
	// 		 Im gonna limit this shit!
	//+RIPPER
	+FORCERADIUSDMG
	SeeSound			"weapons/rocklf"
	DeathSound			"weapons/rocklx"
	Obituary			"%o rode %k's rocket."
	
	States
	{
		Spawn:
			MIS2 A 2 bright
			TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",0,0,0,2,0)
			goto Spawn
		Death:
			MISL B 8 bright A_Explode(125,160,0,0,64)

			// Now this is what i call exploder...
			//TNT1 A 0 a_SpawnDebris("Rocket_Exploder_Debris")
		TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",24,24,0,2,0)
		TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",24,-24,0,2,0)
		
		TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",0,-24,0,2,0)
		TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",0,24,0,2,0)
		TNT1 A 0 A_CustomMissile("Rocket_Exploder_Trail",12,12,0,2,0)

			MISL C 6 bright 
			//TNT1 A 6
			MISL D 4 bright
			stop
	}
}

ACTOR Rocket_Exploder_Trail
{	
	radius 1
	height 1
	scale 0.5
	speed 0
	Renderstyle add
	Alpha 0.7
	
	+ACTIVATEIMPACT
	+ACTIVATEPCROSS
	+FORCERADIUSDMG
	+NODAMAGETHRUST
	+MTHRUSPECIES
	+NOGRAVITY
	
	Damage				16
	DamageType			MarineFire2
	
	States
	{
		Spawn:
			//MISL B 0 bright A_PlaySound("weapons/rocklx")
			MISL BCD 3 A_Explode(16,64,0,0,24)
			stop
	}
}
